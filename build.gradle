plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '2.3.0'
    id 'org.springframework.boot' version '3.5.10'
    id 'org.jetbrains.kotlin.plugin.spring' version '2.3.0'
    id 'jacoco'
}

repositories {
    mavenCentral()
    mavenLocal()
}

jacocoTestReport {
    dependsOn test
    reports {
        csv.required = true
    }
}

dependencies {
    implementation platform('org.springframework.boot:spring-boot-dependencies:3.5.10')
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-graphql"
    implementation 'org.jetbrains.kotlin:kotlin-reflect:2.3.10'
    implementation 'jakarta.validation:jakarta.validation-api:3.1.1'

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:6.0.2'
    testImplementation 'org.junit.jupiter:junit-jupiter:6.0.2'

    testImplementation("io.specmatic.enterprise:executable:${specmaticEnterpriseVersion}")
    testImplementation 'org.testcontainers:junit-jupiter:1.21.4'
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
        exceptionFormat = 'full'
    }

    afterSuite { desc, result ->
        if (!desc.parent) {
            def resultLine = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            def lineLength = resultLine.length()
            def separator = "-" * lineLength

            println separator
            println resultLine
            println separator
        }
    }
}

group = 'in.specmatic.sample'
version = '1.0'

configurations {
    configureEach {
        exclude group: 'ch.qos.logback', module: 'logback-classic'
        exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'
    }
}
